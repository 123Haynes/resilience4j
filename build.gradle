buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.jfrog.bintray'
apply from: 'gradle/publishing.gradle'

group = 'io.github.robwin'
sourceCompatibility = 1.8
version = '0.1.6'
description = 'A CircuitBreaker pattern implementation for Java 8 and functional programming-'

repositories {
    mavenCentral()
}

dependencies {
    compile "com.javaslang:javaslang:1.2.2"
    compile "io.dropwizard.metrics:metrics-core:3.1.2"
    testCompile "junit:junit:4.11"
    testCompile "org.assertj:assertj-core:3.0.0"
    testCompile "ch.qos.logback:logback-classic:0.9.26"
    testCompile "io.dropwizard.metrics:metrics-healthchecks:3.1.2"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "io.projectreactor:reactor-stream:2.0.3.RELEASE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.coveralls {
    dependsOn 'check'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}